plugins {
    id 'java-library'
    id 'io.freefair.lombok' version '6.5.0.2'
    id 'maven-publish'
    id 'signing'
}

group 'app.knock.api'
version "${version}"

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    api 'com.squareup.okhttp3:okhttp:4.10.0'
    api 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

task integrationTest(type: Test) {
    include '**/*IT.class'
    useJUnitPlatform()
}

test {
    exclude '**/*IT.class'
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                scm {
                    connection = 'scm:git:git://github.com/knocklabs/knock-java.git'
                    developerConnection = 'scm:git:ssh://git@github.com:knocklabs/knock-java.git'
                    url = 'https://knock.app/'
                }
                developers {
                    developer {
                        id = 'knock-support'
                        name = 'Knock Support'
                        email = 'support@knock.app'
                    }
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
            }
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
            }
        }
    }
    repositories {
        maven {
            url = project.hasProperty('snapshot') ? project.property("repo.snapshots.url") : project.property("repo.releases.url")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
//    def keyId = System.getenv("GPG_KEY_ID")
    def signingKey = System.getenv("GPG_SIGNING_KEY")
    def signingPassword = System.getenv("GPG_SIGNING_PASSWORD")
//    useInMemoryPgpKeys(keyId, signingKey, signingPassword)
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.maven
}
